use crate::ast::*;
use crate::ast::decl::*;
use crate::ast::expr::*;

grammar;

// 约束 lexer 的行为
match {
  // 跳过空白符和注释
  r"\s*" => {},
  r"//[^\n\r]*[\n\r]*" => {}, // Skip `// comments`
  r"/\*([^*]|\*[^/])*\*/" => {},  // Skip `/* comments */`
  // 剩下的情况采用默认方式处理
  _
}

Comma<T>: Vec<T> = {
  <mut v:(<T> ",")*> <e: T> => match e {
    None => v,
    Some(e) => {
      v.push(e);
      v
    }
  }
}

// 定义 CompUnit, 其返回值类型为 String
// parser 在解析完成后的行为是返回 FuncDef 的值
pub CompUnit: CompUnit = <func_def: FuncDef> => CompUnit { <> };

FuncDef: FuncDef = {
  <func_type: FuncType> <ident: Ident> "(" ")" <block: Block> => {
    FuncDef { <> }
  }
}

FuncType: FuncType = "int" => FuncType::Int;

Block: Block = "{" <block_items: BlockItem*> "}" => Block { <> };

BlockItem: BlockItem = {
  <decl: Decl> => BlockItem::Decl(decl),
  <stmt: Stmt> => BlockItem::Stmt(stmt),
};

// --------------- DECL START ----------------
Decl: Decl = {
  <const_decl: ConstDecl> => Decl::ConstDecl(const_decl),
  <var_decl: VarDecl> => Decl::VarDecl(var_decl),
}

BType: BType = "int" => BType::Int;

ConstDecl: ConstDecl = {
  "const" <b_type: BType> <const_def: ConstDef> <mut const_defs:("," <ConstDef>)*> ";" => {
    const_defs.insert(0, const_def);
    ConstDecl { b_type, const_defs}
  }
};
ConstDef: ConstDef = <ident: Ident> "=" <const_init_val: ConstInitVal> => {
  ConstDef { ident, const_init_val }
};
ConstInitVal: ConstInitVal = <const_exp: ConstExp> => ConstInitVal { const_exp };
ConstExp: ConstExp = <exp: Exp> => ConstExp { exp };

VarDecl: VarDecl = {
  <b_type: BType> <var_def: VarDef> <mut var_defs:("," <VarDef>)*> ";" => {
    var_defs.insert(0, var_def);
    VarDecl { b_type, var_defs }
  }
};
VarDef: VarDef = {
  <ident: Ident> => VarDef::IdentDefine(ident),
  <ident: Ident> "=" <init_val: InitVal> => VarDef::IdentInitVal(ident, init_val),
};
InitVal: InitVal = <exp: Exp> => InitVal { exp };


// ---------------- DECL END ----------------


// -------------- STMT START ----------------

Stmt: Stmt = {
  <UnmatchedIfStmt> => <>,
  <MatchedStmt> => <>,
}

UnmatchedIfStmt: Stmt = {
  "if" "(" <exp: Exp> ")" <stmt: Stmt> => 
    Stmt::IfElseStmt ( IfCond{exp: exp}, ThenStmt{stmt: Box::new(stmt)}, None ),

  
  "if" "(" <exp: Exp> ")" <stmt: MatchedStmt> "else" <stmt2: UnmatchedIfStmt> => 
    Stmt::IfElseStmt ( IfCond{exp: exp}, ThenStmt{stmt: Box::new(stmt)}, Some(ElseStmt{stmt: Box::new(stmt2)}) ),
}

/*
TODO

if (Exp) Stmt
else if (Exp) Stmt

这种语句无法parse
*/
MatchedStmt: Stmt = {
  <l_val: LVal> "=" <exp: Exp> ";" => Stmt::AssignStmt ( <> ),
  <exp: Exp?> ";" => Stmt::ExpStmt ( <> ),
  <block: Block> => Stmt::BlockStmt ( <> ),
  "return" <exp: Exp> ";" => Stmt::ReturnExp ( <> ),
  "if" "(" <exp: Exp> ")" <stmt1: MatchedStmt> "else" <stmt2: MatchedStmt> => 
    Stmt::IfElseStmt ( IfCond{exp: exp}, ThenStmt{stmt: Box::new(stmt1)}, Some(ElseStmt{stmt: Box::new(stmt2)}) ),
}

// --------------- STMT END ----------------

// --------------- EXP ----------------
Exp: Box<Exp> = <l_or_exp: LOrExp> => Box::new(Exp{<>});

LOrExp: Box<LOrExp> = {
  <l_and_exp: LAndExp> => Box::new(LOrExp::LAndExp(<>)),
  <l_or_exp: LOrExp> "||" <l_and_exp: LAndExp> => {
    Box::new(LOrExp::LOrExpOpLAndExp(<>))
  }
}

LAndExp: Box<LAndExp> = {
  <eq_exp: EqExp> => Box::new(LAndExp::EqExp(<>)),
  <l_and_exp: LAndExp> "&&" <eq_exp: EqExp> => {
    Box::new(LAndExp::LAndExpOpEqExp(<>))
  }
}

EqExp: Box<EqExp> = {
  <rel_exp: RelExp> => Box::new(EqExp::RelExp(<>)),
  <eq_exp: EqExp> <op: EqExpOp> <rel_exp: RelExp> => {
    Box::new(EqExp::EqExpOpRelExp(<>))
  }
}

EqExpOp: EqExpOp = {
  "==" => EqExpOp::Eq,
  "!=" => EqExpOp::Ne,
}

RelExp: Box<RelExp> = {
  <add_exp: AddExp> => Box::new(RelExp::AddExp(<>)),
  <rel_exp: RelExp> <op: RelExpOp> <add_exp: AddExp> => {
    Box::new(RelExp::RelExpOpAddExp(<>))
  }
}

RelExpOp: RelExpOp = {
  "<" => RelExpOp::Lt,
  ">" => RelExpOp::Gt,
  "<=" => RelExpOp::Le,
  ">=" => RelExpOp::Ge,
}

AddExp: Box<AddExp> = {
  <mul_exp: MulExp> => Box::new(AddExp::MulExp(<>)),
  <add_exp: AddExp> <op: AddOp> <mul_exp: MulExp> => {
    Box::new(AddExp::AddExpOpMulExp(<>))
  }
}

MulExp: Box<MulExp> = {
  <unary_exp: UnaryExp> => Box::new(MulExp::UnaryExp(<>)),
  <mul_exp: MulExp> <op: MulOp> <unary_exp: UnaryExp> => {
    Box::new(MulExp::MulExpOpUnaryExp(<>))
  }
}

UnaryExp: Box<UnaryExp> = {
  <primary_exp: PrimaryExp> => Box::new(UnaryExp::PrimaryExp(<>)),
  <unary_op: UnaryOp> <unary_exp: UnaryExp> => {
    Box::new(UnaryExp::UnaryOpAndExp(<>))
  },
}

PrimaryExp: Box<PrimaryExp> = {
  "(" <exp: Exp> ")" => Box::new(PrimaryExp::Exp(<>)),
  <number: Number> => Box::new(PrimaryExp::Number(<>)),
  <l_val: LVal> => Box::new(PrimaryExp::LVal(<>)),
}

LVal: LVal = <ident: Ident> => LVal { <> };

AddOp: AddOp = {
  "+" => AddOp::Plus,
  "-" => AddOp::Minus,
}

MulOp: MulOp = {
  "*" => MulOp::Mul,
  "/" => MulOp::Div,
  "%" => MulOp::Mod,
}

UnaryOp: UnaryOp = {
  "+" => UnaryOp::Plus,
  "-" => UnaryOp::Minus,
  "!" => UnaryOp::Not,
}

Number: i32 = <num: IntConst> => <>;

// 如果匹配到标识符, 就返回这个字符串
// 一对尖括号在此处指代的是正则表达式匹配到的字符串 (&str)
// 关于尖括号到底代表什么, 请 RTFM
Ident: String = r"[_a-zA-Z][_a-zA-Z0-9]*" => <>.to_string();

// 对整数字面量的处理方式: 把匹配到的字符串按对应进制转换成数字
IntConst: i32 = {
  r"[1-9][0-9]*" => i32::from_str_radix(<>, 10).unwrap(),
  r"0[0-7]*" => i32::from_str_radix(<>, 8).unwrap(),
  r"0[xX][0-9a-fA-F]+" => i32::from_str_radix(&<>[2..], 16).unwrap(),
}