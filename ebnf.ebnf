CompUnit  ::= [CompUnit] FuncDef;

FuncDef   ::= FuncType IDENT "(" [FuncFParams] ")" Block;
FuncType  ::= "int" | "void";
FuncFParams ::= FuncFParam { "," FuncFParam };
FuncFParam ::= BType IDENT;

# Const part
Decl ::= ConstDecl | VarDecl;

ConstDecl ::= "const" BType ConstDef { "," ConstDef } ";";
BType     ::= "int";
ConstDef  ::= IDENT "=" ConstInitVal;
ConstInitVal ::= ConstExp;
ConstExp ::= Exp;

VarDecl ::= BType VarDef { "," VarDef } ";";
VarDef  ::= IDENT | IDENT "=" InitVal;
InitVal ::= Exp;

Block     ::= "{" {BlockItem} "}";
BlockItem ::= Decl | Stmt;
Stmt      ::= LVal "=" Exp ";" 
    | [Exp] ";"
    | Block
    | "return" Exp ";";
    | "if" "(" Exp ")" Stmt "else" Stmt;
    | "if" "(" Exp ")" Stmt;
    | "while" "(" Exp ")" Stmt;


# -------- Expr part -----------

Exp       ::= LOrExp;

LOrExp ::= LAndExp | LOrExp "||" LAndExp;
LAndExp ::= EqExp | LAndExp "&&" EqExp;

EqExp ::= RelExp | EqExp ("==" | "!=") RelExp;
RelExp ::= AddExp | RelExp ("<" | ">" | "<=" | ">=") AddExp;
MulExp ::= UnaryExp | MulExp ("*" | "/" | "%") MulExp;
AddExp ::= MulExp | AddExp ("+" | "-") MulExp;

UnaryExp  ::= PrimaryExp | UnaryOp UnaryExp | IDENT "(" [FuncRParams] ")";
UnaryOp ::= "+" | "-" | "!";
FuncRParams ::= Exp { "," Exp };

PrimaryExp ::= "(" Exp ")" | Number | LVal;
LVal ::= IDENT;
Number    ::= INT_CONST;
